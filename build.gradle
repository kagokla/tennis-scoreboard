plugins {
    id 'jacoco'
    id 'java'
    alias(libs.plugins.lombok)
}

group = 'com.github.kagokla'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

compileJava {
    options.release = 21
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes "Main-Class": "com.github.kagokla.scoreboard.Main"
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

dependencies {
    implementation(libs.bundles.apache.common.helpers)
    implementation(libs.logback)

    // TEST
    testImplementation(libs.assertj.core)
    testImplementation(libs.junit)
}

test {
    useJUnitPlatform()
    testLogging {
        events 'FAILED', 'SKIPPED', 'PASSED'
    }
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: "com/github/kagokla/**/Main.class")
        }))
    }
}